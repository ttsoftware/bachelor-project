/* Converts the INI format to JSON
   Assumes that comments and blank lines have been stripped from the file. */

ini2json := "{\n" sections "}\n"

sections := section "," <\n> sections
          | section "\n" ~<\n?>

section := ind "\"" header ~<\n> "\": {\n" keyvalues ind "}"
         | ind "\"" header "\": {\n" ind "}"

header := skipWs ~<\[> <[^\n\]]*> ~<]> skipWs

keyvalue := ind ind key ": " ~<=> value

keyvalues := keyvalue "," <\n> keyvalues
           | keyvalue "\n"

key := skipWs "\"" <[^; \t=\[\n]*> "\"" skipWs

value := skipWs <"[^\n]*"> skipWs
       | skipWs "\"" escapedValue "\"" skipWs

escapedValue := ""
              | escapedChar escapedValue

escapedChar := ~<\\> "\\\\"
             | ~<"> "\\\""
             | <[^\n]>

skipWs := ~<[ \t]*>

ind := "    "
